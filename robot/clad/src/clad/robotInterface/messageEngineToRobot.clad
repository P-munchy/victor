/** Clad definitions of all messages sent from the Cozmo engine to the robot and from one part of the robot to another.
 * This file name is now out of date since it should be messageToRobot since messages may not be coming from the engine.
 *
 * Used in the engine, all three robot processors and the test fixture, however, we don't have to build and flash in
 * lockstep because of strict tag space partitioning.
 */

#include "clad/audio/audioMessage.clad"
#include "clad/types/imageTypes.clad"
#include "clad/types/robotStatusAndActions.clad"
#include "clad/types/ledTypes.clad"
#include "clad/types/animationTypes.clad"
#include "clad/types/robotTestModes.clad"
#include "clad/types/dockingSignals.clad"
#include "clad/types/imu.clad"
#include "clad/types/controllerChannels.clad"
#include "clad/robotInterface/messageToActiveObject.clad"
#include "clad/robotInterface/messageFromActiveObject.clad"
#include "clad/robotInterface/otaMessages.clad"
#include "clad/types/fwTestMessages.clad"
#include "clad/types/cameraFOVInfo.clad"
#include "clad/types/motorTypes.clad"


namespace Anki {
namespace Cozmo {
  
  structure SetPropSlot
  {
    uint_32 factory_id,
    uint_8  slot
  }

  message SetCubeGamma
  {
    uint_8  gamma
  }

namespace RobotInterface {

/** Pipe from engine to robot.
 * This cannot be an auto union because we need explicit tag values to partition the RTIP and WiFi code
 * and to make debugging from message dumps more sane.
 */

message StartMotorCalibration
{
  bool calibrateHead,
  bool calibrateLift
}

message DriveWheels
{
  float_32 lwheel_speed_mmps,
  float_32 rwheel_speed_mmps,
  float_32 lwheel_accel_mmps2 = 0.0,
  float_32 rwheel_accel_mmps2 = 0.0
}

message DriveWheelsCurvature
{
  float_32 speed,               // Target speed in mm/sec (or rad/s if curvatureRadius_mm == 0)
                                // If curvatureRadius_mm == 0, the speed is in rad/s where +ve means CCW rotation.
  float_32 accel,               // Acceleration to approach target speed in mm/sec^2
                                // (or rad/s^2 if curvatureRadius_mm == 0)
                                // 0: Max acceleration

  int_16 curvatureRadius_mm     // Radius of arc to drive
                                // u16_MAX or u16_MIN: Straight
                                // +ve: curves left, -ve: curves right
                                // 0: Point turn
}

message MoveLift
{
  float_32 speed_rad_per_sec,
}

message MoveHead
{
  float_32 speed_rad_per_sec,
}

message SetLiftHeight
{
  float_32 height_mm,
  float_32 max_speed_rad_per_sec,
  float_32 accel_rad_per_sec2,
  float_32 duration_sec,
}

message SetHeadAngle
{
  float_32 angle_rad,
  float_32 max_speed_rad_per_sec,
  float_32 accel_rad_per_sec2,
  float_32 duration_sec,
}

message HeadAngleUpdate
{
  float_32 newAngle,
}

// This is a turn in place to an absolute angle
message SetBodyAngle
{
  float_32 angle_rad,              // Absolute angle the robot should achieve at the end of the turn.
  float_32 max_speed_rad_per_sec,  // Maximum speed of the turn (sign determines the direction of the turn)
  float_32 accel_rad_per_sec2,     // Accel/decel to use during the turn
  float_32 angle_tolerance,
  uint_16 num_half_revolutions,    // Used for turns greater than 180 degrees (e.g. "turn 720 degrees")
  bool use_shortest_direction      // If true, robot should take the shortest path to angle_rad and disregard
                                   // the sign of max_speed and num_half_revolutions. If false, consider sign
                                   // of max_speed and num_half_revolutions to determine which direction to
                                   // turn and how far to turn (e.g. for turns longer than 360 deg)
}

message TurnInPlaceAtSpeed
{
  float_32 speed_rad_per_sec,
  float_32 accel_rad_per_sec2,
}

message StopAllMotors
{
}

message ClearPath
{
  uint_16 pathID,
}

message PathSegmentSpeed
{
  float_32 target,
  float_32 accel,
  float_32 decel,
}

message AppendPathSegmentLine
{
  float_32 x_start_mm,
  float_32 y_start_mm,
  float_32 x_end_mm,
  float_32 y_end_mm,
  PathSegmentSpeed speed,
}

message AppendPathSegmentArc
{
  float_32 x_center_mm,
  float_32 y_center_mm,
  float_32 radius_mm,
  float_32 startRad,
  float_32 sweepRad,
  PathSegmentSpeed speed,
}

message AppendPathSegmentPointTurn
{
  float_32 x_center_mm,
  float_32 y_center_mm,
  float_32 targetRad,
  float_32 angleTolerance,
  PathSegmentSpeed speed, // targetSpeed indicates rotational speed in rad/s
  bool     useShortestDir
}

message TrimPath
{
  uint_8 numPopFrontSegments,
  uint_8 numPopBackSegments,
}

message ExecutePath
{
  uint_16 pathID,
  bool    useManualSpeed,
}

message AbsoluteLocalizationUpdate
{
  uint_32 timestamp,
  uint_32 pose_frame_id,
  uint_32 origin_id,
  float_32 xPosition,
  float_32 yPosition,
  float_32 headingAngle,
}

// Used only for testing with simulated robots. Engine will never send this message if it is talking
// talking to a simulated robot
message ForceDelocalizeSimulatedRobot
{

}

message SyncTime
{
  uint_32 syncTime,

  // Other random things to send to robot on init
  // which can eventually be made constant
  float_32 driveCenterOffset
}

message ControllerGains
{
  float_32 kp,
  float_32 ki,
  float_32 kd,
  float_32 maxIntegralError,
  ControllerChannel controller
}

message RollActionParams
{
   float_32 liftHeight_mm,
   float_32 driveSpeed_mmps,
   float_32 driveAccel_mmps2,
   uint_32 driveDuration_ms,
   float_32 backupDist_mm,
}

message SetMotionModelParams
{
  float_32 slipFactor,
}

message BackpackSetLayer
{
  uint_8 layer
}

message GetManufacturingInfo
{
}

message AbortAnimation
{
  uint_8 tag
}

message CheckLiftLoad
{
}

// Enable/Disable power to lift completely.
// Once disabled, must be re-enabled before motor can be commanded again.
// Only works for MOTOR_LIFT and MOTOR_HEAD.
message EnableMotorPower
{
  MotorID motorID,
  bool    enable
}

message EnableReadToolCodeMode
{
  float_32 headPower,
  float_32 liftPower,
  bool enable
}

message EnableStopOnCliff
{
  bool enable
}

message SetCliffDetectThreshold
{
  uint_16 detectLevel
}

message EnableBraceWhenFalling
{
  bool enable
}

// ========== V2 Animation process messages ===========

// TODO: Make animation files referencable by integer tag so we don't have to send a string.
message RequestAvailableAnimations
{
}

message PlayAnim
{
  uint_32 animID,
  uint_32 numLoops = 1,
  uint_8  tag
}

message LockAnimTracks
{
  uint_8 whichTracks    // See AnimTrackFlags
}

message RecordHeading
{
}

message TurnToRecordedHeading
{
  int_16 offset_deg,        // Offset from recorded angle
  int_16 speed_degPerSec,
  int_16 accel_degPerSec2,
  int_16 decel_degPerSec2,
  uint_16 tolerance_deg,
  uint_16 numHalfRevs,
  bool useShortestDir,
}

message SetBackpackLights
{
  LightState lights[3],
  uint_8 layer = 0      // 0 is BPL_USER
}

// ====================================================

enum uint_32 ToRobotAddressSpace
{
  GLOBAL_INVALID_TAG = 0,
  TO_BODY_START = 0x01,
  TO_BODY_END   = 0x2F,
  TO_RTIP_START = 0x30,
  TO_RTIP_END   = 0x7F,
  ANIM_BUFFER_START = 0x8E,
  ANIM_BUFFER_END   = 0x9C,
  ANIM_RT_START = 0x90,
  ANIM_RT_END   = 0x9A,
  TO_WIFI_START = 0x80,
  TO_WIFI_END   = 0xAF,
  TO_ENG_START  = 0xB0,
  TO_ENG_UNREL  = 0xF0,
  TO_ENG_END    = 0xFE
}

/// *************** Overall message pipe ******************************* ///

/** Pipe from engine to robot and within the robot
 * This cannot be an auto union because we need explicit tag values to partition the messages between the engine (not
 * included in this file), WiFi, RTIP, body and test fixture. And significantly to make debugging from message dumps
 * more sane.
 */
union EngineToRobot
{
  // 0x00 Is invalid message

  // 0x01 to 0x2f: Messages to the body / test fixture
  BackpackSetLayer              setBackpackLayer            = 0x02,


  // TODO: These cube messages will need to be modified and moved 
  //       to a separate file once we update block connection logic
  //       for BLE
  CubeLights                    setCubeLights               = 0x04,
  SetPropSlot                   setPropSlot                 = 0x05,

  StreamObjectAccel             streamObjectAccel           = 0x08,

  SetCubeGamma                  setCubeGamma                = 0x0C,
  CubeID                        setCubeID                   = 0x10,
  
  GetManufacturingInfo          getMfgInfo                  = 0x25,

  // 0x30 to 0x7f: To RTIP
  //          FREE SLOT!!!                                  = 0x30,
  //          FREE SLOT!!!                                  = 0x31,
  DriveWheels                   drive                       = 0x32,
  DriveWheelsCurvature          driveCurvature              = 0x33,
  MoveLift                      moveLift                    = 0x34,
  MoveHead                      moveHead                    = 0x35,
  SetLiftHeight                 liftHeight                  = 0x36,
  SetHeadAngle                  headAngle                   = 0x37,
  HeadAngleUpdate               headAngleUpdate             = 0x38,
  SetBodyAngle                  setBodyAngle                = 0x39,
  TurnInPlaceAtSpeed            turnInPlaceAtSpeed          = 0x3A,
  StopAllMotors                 stop                        = 0x3B,
  ClearPath                     clearPath                   = 0x3C,
  AppendPathSegmentLine         appendPathSegLine           = 0x3D,
  AppendPathSegmentArc          appendPathSegArc            = 0x3E,
  AppendPathSegmentPointTurn    appendPathSegPointTurn      = 0x3F,
  TrimPath                      trimPath                    = 0x40,
  ExecutePath                   executePath                 = 0x41,
  DockWithObject                dockWithObject              = 0x42,
  AbortDocking                  abortDocking                = 0x43,
  PlaceObjectOnGround           placeObjectOnGround         = 0x44,
  AbsoluteLocalizationUpdate    absLocalizationUpdate       = 0x45,
  StartControllerTestMode       startControllerTestMode     = 0x46,
  ControllerGains               setControllerGains          = 0x47,
  DockingErrorSignal            dockingErrorSignal          = 0x48,
  CarryStateUpdate              setCarryState               = 0x49,
  IMURequest                    imuRequest                  = 0x4A,
  SyncTime                      syncTime                    = 0x4B,
  
  CheckLiftLoad                 checkLiftLoad               = 0x4F,
  EnableMotorPower              enableMotorPower            = 0x50,
  SetMotionModelParams          setMotionModelParams        = 0x51,
  
  SetCliffDetectThreshold       setCliffDetectThreshold     = 0x54,
  EnableReadToolCodeMode        enableReadToolCodeMode      = 0x55,
  
  StartMotorCalibration         startMotorCalibration       = 0x58,
  RollActionParams              rollActionParams            = 0x59,
  CameraFOVInfo                 cameraFOVInfo               = 0x5A,
  //          FREE SLOT!!!                                  = 0x5B,
  //          FREE SLOT!!!                                  = 0x5C,
  //          FREE SLOT!!!                                  = 0x5D,
  //          FREE SLOT!!!                                  = 0x5E,
  //          FREE SLOT!!!                                  = 0x5F,
  EnableStopOnCliff             enableStopOnCliff           = 0x60,
  ForceDelocalizeSimulatedRobot forceDelocalizeSimulatedRobot = 0x61, // Used for simulation only, no-op for physical
  EnableBraceWhenFalling        enableBraceWhenFalling      = 0x63,
  

  // V2 Audio
  AudioEngine::Multiplexer::PostAudioEvent        postAudioEvent       = 0x70,
  AudioEngine::Multiplexer::StopAllAudioEvents    stopAllAudioEvents   = 0x71,
  AudioEngine::Multiplexer::PostAudioGameState    postAudioGameState   = 0x72,
  AudioEngine::Multiplexer::PostAudioSwitchState  postAudioSwitchState = 0x73,
  AudioEngine::Multiplexer::PostAudioParameter    postAudioParameter   = 0x74,

  // 0x80 to 0xAF: To WiFi in V1. To Anim Process in V2
  //       FREE SLOT!!!                                     = 0x80,
  //       FREE SLOT!!!                                     = 0x81,
  PlayAnim                      playAnim                    = 0x82,   // V2
  LockAnimTracks                lockAnimTracks              = 0x83,   // V2
  RecordHeading                 recordHeading               = 0x84,   // V2
  TurnToRecordedHeading         turnToRecordedHeading       = 0x85,   // V2
  //       FREE SLOT!!!                                     = 0x86,
  //       FREE SLOT!!!                                     = 0x87,
  //       FREE SLOT!!!                                     = 0x89,
  SetBackpackLights             setBackpackLights           = 0x8A,   // V2
  //       FREE SLOT                                        = 0x8B,
  RequestAvailableAnimations    requestAvailableAnimations  = 0x8C,   // V2
  AbortAnimation                abortAnimation              = 0x8D,

  // 0xB0 to 0xFE: To engine, see messageRobotToEngine.clad
}


} // namespace Robot Interface
} // namespace Cozmo
} // namespace Anki
