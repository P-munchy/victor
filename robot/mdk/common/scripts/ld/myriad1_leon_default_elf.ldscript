/*  This is a Leon Default Linker script 
 *  Should be used as an included linker script in top level scripts 
 *
 *  Addresses defined in common/drivers/system/config/memmap.S
 */

ENTRY( start );

SECTIONS {
  . = __LRAM;
  .sys.traps ALIGN(0x1000) : {
    *(.sys.text.traps)
  }
  .sys.text ALIGN(4) : { 
    *(.sys.text.start)
  }
  .text ALIGN(4) : { *(.text*) }
  .sys.rodata ALIGN(4) : { *(.sys.rodata*) }
  .rodata ALIGN(4) : { *(.rodata*) }
  .data ALIGN(4) : { *(.data*) }

  .bss (NOLOAD) : { 
    . = ALIGN(8);
    __bss_start = . ;
    *(.bss*) *(COMMON)
    . = ALIGN(8);
    __bss_end = . ;
  }
  __EMPTY_RAM = ALIGN(4);
  .init_text : {
    *(.init_text*)
    __init_text_end = ALIGN(4);
  }
  
  . = __LRAM + 0x8000 - 0xc0;
  .sys.bss ALIGN(4) (NOLOAD) : { 
    . = ALIGN(8);
    __sys_bss_start = . ;
    *(.sys.bss*) 
    . = ALIGN(8);
    __sys_bss_end = . ;
  }
  ASSERT( __bss_end < __sys_bss_start , "LRAM content overflows 32KB" )
  __RAM_TOP = __LRAM + 0x8000;
  
  __MIN_STACK_SIZE = DEFINED( __MIN_STACK_SIZE ) ? __MIN_STACK_SIZE : 3584;
  __MIN_INIT_STACK_SIZE = DEFINED( __MIN_INIT_STACK_SIZE ) ? __MIN_INIT_STACK_SIZE : 1024;
  
  __STACK_SIZE = ABSOLUTE(__sys_bss_start - __bss_end);
  ASSERT( __STACK_SIZE > __MIN_STACK_SIZE , "Leon Code + data too big. Stack space left is smaller than the required minimum (3.5 KB)!")
  
  __INIT_STACK_SIZE = ABSOLUTE(__sys_bss_start - __init_text_end);
  ASSERT( __INIT_STACK_SIZE > __MIN_INIT_STACK_SIZE , "Stack available to the init code is smaller than the specified minimum (which by default is 1 KB)!")
  
  /DISCARD/ : {
    *(.stab .stabstr .comment .gnu.attributes)
  }
  
  /* 
   * locate the special sections in correct place  
   * These sections are required to ensure Myriad is initialized correctly and must be loaded first   
   */	
  .l2.mode    0x800F0000 : { KEEP(*(.l2.mode)) }           /* L2 cache mode                */
  .cmx.ctrl   0xAE000020 : { KEEP(*(.cmx.ctrl)) }          /* CMX default layout           */
  .winregs.S0 0x80140010 : { KEEP(*(.winregs.S0)) }       /* Window registers for Shave 0 */
  .winregs.S1 0x80150010 : { KEEP(*(.winregs.S1)) }       /* Window registers for Shave 1 */
  .winregs.S2 0x80160010 : { KEEP(*(.winregs.S2)) }       /* Window registers for Shave 2 */
  .winregs.S3 0x80170010 : { KEEP(*(.winregs.S3)) }       /* Window registers for Shave 3 */
  .winregs.S4 0x80180010 : { KEEP(*(.winregs.S4)) }       /* Window registers for Shave 4 */
  .winregs.S5 0x80190010 : { KEEP(*(.winregs.S5)) }       /* Window registers for Shave 5 */
  .winregs.S6 0x801A0010 : { KEEP(*(.winregs.S6)) }       /* Window registers for Shave 6 */
  .winregs.S7 0x801B0010 : { KEEP(*(.winregs.S7)) }       /* Window registers for Shave 7 */
  
}
